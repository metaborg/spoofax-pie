module gen/kotlin-ceres/exp

imports

  libspoofax/stratego/debug
  nabl2/api

  signatures/-
  signatures/lex/-
  signatures/impl/-
  
  statsem/program
  
  gen/kotlin-ceres/def
  gen/kotlin-ceres/binder
  gen/kotlin/type2kotlin
  gen/idtype-tuples
  gen/util
  gen/debug

rules 

  try-p2c-exp = pipe-try-debug(p2c-exp|"p2c-exp") <+ !"Any()"
  
rules // Nullable
  
  p2c-exp:
    ToNullable(exp) -> <p2c-exp> exp
    where
      NullableTy(ty) := <pipe-ast-type> exp

  p2c-exp:
    ToNullable(exp) -> $[[expStr] as [tySig]?]
    with
      ty := <pipe-ast-type> exp
    where
      not(NullableTy(_) := ty)
    with
      tySig  := <t2k-sig> ty
    ; expStr := <p2c-exp> exp

  p2c-exp:
    ToNonNullable(exp) -> $[[expStr]!!]
    with
      expStr := <p2c-exp> exp

  p2c-exp:
    Not(exp) -> $[![expStr]]
    with
      expStr := <p2c-exp> exp

rules // Binary expressions

  p2c-binexp(|symbol):
    (lExp, rExp) -> $[[lExpStr] [symbol] [rExpStr]]
    with
      lExpStr := <try-p2c-exp> lExp
    ; rExpStr := <try-p2c-exp> rExp
  
  p2c-exp: Eq(lExp, rExp) -> <p2c-binexp(|"==")> (lExp, rExp)
  p2c-exp: Neq(lExp, rExp) -> <p2c-binexp(|"!=")> (lExp, rExp)
  p2c-exp: Lor(lExp, rExp) -> <p2c-binexp(|"||")> (lExp, rExp)
  p2c-exp: Land(lExp, rExp) -> <p2c-binexp(|"&&")> (lExp, rExp)

rules // If else

  p2c-exp:
    If(condExp, exp) -> $[if([condExpStr]) [expStr]]
    with
      condExpStr := <try-p2c-exp> condExp
    ; expStr     := <try-p2c-exp> exp

  p2c-exp:
    IfElse(condExp, trueExp, falseExp) -> $[if([condExpStr]) [trueExpStr] else [falseExpStr]]
    with
      condExpStr  := <try-p2c-exp> condExp
    ; trueExpStr  := <try-p2c-exp> trueExp
    ; falseExpStr := <try-p2c-exp> falseExp

  p2c-exp:
    Add(lExp, rExp) -> $[[lExpStr] + [rExpStr]]
    with
      lExpStr := <try-p2c-exp> lExp
    ; rExpStr := <try-p2c-exp> rExp
    
rules // List comprehension

  p2c-exp:
    e@ListComprehensionExp(mapExp, binder, listExp) -> $[[listExpStr].map {[binderStr] -> [mapExpStr]}.toCollection([tyStr]())]
    with
      mapExpStr  := <try-p2c-exp> mapExp
    ; binderStr  := <p2c-binder> binder
    ; listExpStr := <try-p2c-exp> listExp
    ; ty         := <pipe-ast-type> e
    ; tyStr      := <t2k-sig> ty

rules // References

  p2c-exp:
    r@Ref(varId) -> <p2c-ref-add-transient-val(|varId)> r 
    where
      LocalVarKind() := <pipe-ast-ref-prop(|"Var", "kind")> varId
  
  p2c-exp: 
    Ref(varId) -> <fail>
    where
      InputVarKind() := <pipe-ast-ref-prop(|"Var", "kind")> varId
    ; 0 := <InputKind>
      
  p2c-exp: 
    r@Ref(varId) -> <p2c-ref-add-transient-val(|"input")> r
    where
      InputVarKind() := <pipe-ast-ref-prop(|"Var", "kind")> varId
    ; 1 := <InputKind>
      
  p2c-exp: 
    r@Ref(varId) -> <p2c-ref-add-transient-val(|$[input.[varId]])> r
    where
      InputVarKind() := <pipe-ast-ref-prop(|"Var", "kind")> varId
    ; 2 := <InputKind>

  p2c-ref-add-transient-val(|str):
    ref -> $[[str].v]
    where
      <p2c-ref-is-transient> ref
    
  p2c-ref-add-transient-val(|str):
    ref -> str
    where
      <not(p2c-ref-is-transient)> ref
  
  p2c-ref-is-transient = 
      pipe-ast-type
    ; (?DataTy(<id>) <+ ?NullableTy(DataTy(<id>)))
    ; pipe-occ-prop(|"mods")
    ; collect-one(?Transient())

rules // External function calls

  p2c-exp:
    Call(funcId, exps) -> $[[javaClassId].[javaMethodId]([expStr])]
    where
      JavaStaticFuncKind() := <pipe-ast-ref-prop(|"Func", "kind")> funcId
    with
      javaClassId  := <pipe-ast-ref-prop(|"Func", "javaClassId")> funcId
    ; javaMethodId := <pipe-ast-ref-prop(|"Func", "javaMethodId")> funcId
    ; expStr       := <map(try-p2c-exp); pipe-concat-comma> exps
    
  p2c-exp:
    Call(funcId, exps) -> $[[kotlinClassOrPackageId].[kotlinFuncId]([expStr])]
    where
      KotlinFuncKind() := <pipe-ast-ref-prop(|"Func", "kind")> funcId
    with
      kotlinClassOrPackageId := <pipe-ast-ref-prop(|"Func", "kotlinClassOrPackageId")> funcId
    ; kotlinFuncId           := <pipe-ast-ref-prop(|"Func", "kotlinFuncId")> funcId
    ; expStr                 := <map(try-p2c-exp); pipe-concat-comma> exps

rules // Data type function calls

  p2c-exp: 
    QCall(exp, funcId, argsExps) -> $[[expStr].[funcId]([argsExpStr])]
    with
      argsExpStr := <map(try-p2c-exp); pipe-concat-comma> argsExps
    ; expStr     := <try-p2c-exp> exp

rules // Ceres builder calls
  
  p2c-exp:
    c@Call(funcId, _) -> <p2c-exp-ceres-call(|javaClassId)> c
    where
      CeresBuilderFuncKind() := <pipe-ast-ref-prop(|"Func", "kind")> funcId
    with
      javaClassId := <pipe-ast-ref-prop(|"Func", "javaClassId")> funcId
    
  p2c-exp: 
    c@Call(funcId, _) -> <p2c-exp-ceres-call(|javaClassId)> c 
    where
      PipelineFuncKind() := <pipe-ast-ref-prop(|"Func", "kind")> funcId
    with
      javaClassId := <pipe-sanitize-class-id> funcId


  p2c-exp-ceres-call(|javaClassId):
   Call(funcId, exps) -> $[requireOutput([javaClassId]::class.java, [expStr])]
    with
      expStr := <p2c-exp-ceres-call-inputexp(|javaClassId)> exps


  p2c-exp-ceres-call-inputexp(|javaClassId):
    exps -> "None.instance"
    where
      0 := <length> exps

  p2c-exp-ceres-call-inputexp(|javaClassId):
    exps -> expStr
    where
      1 := <length> exps
    with
      [exp]  := exps
    ; expStr := <try-p2c-exp> exp
    
  p2c-exp-ceres-call-inputexp(|javaClassId):
    exps -> $[[javaClassId].Input([expStr])]
    where
      length := <length> exps
    ; <gt> (length, 1)
    with
      expStr := <map(try-p2c-exp); pipe-concat-comma> exps

rules // Path operations

  p2c-exp:
    ListPath(exp, filterOpt) -> $[requireOutput(ListContents::class.java, ListContents.Input([expStr], [filterStr]))]
    with
      expStr    := <p2c-exp> exp
    ; filterStr := <p2c-filter-match> filterOpt
    
  p2c-exp:
    WalkPath(exp, filterOpt) -> $[requireOutput(WalkContents::class.java, WalkContents.Input([expStr], [filterStr]))]
    with
      expStr    := <p2c-exp> exp
    ; filterStr := <p2c-filter-walk> filterOpt
      
  p2c-filter-match:
    None() -> $[null]
  p2c-filter-match:
    Regex(exp) -> $[PPaths.regexPathMatcher([expStr])]
    with
      expStr := <try-p2c-exp> exp
  p2c-filter-match:
    _ -> $[PPaths.patternsPathMatcher([expStr])]
    where ?Pattern(exp) + ?Patterns(exp)
    with
      expStr := <try-p2c-exp> exp
  p2c-filter-match:
    _ -> $[PPaths.extensionsPathMatcher([expStr])]
    where ?Extension(exp) + ?Extensions(exp)
    with
      expStr := <try-p2c-exp> exp
      
  p2c-filter-walk:
    None() -> $[null]
  p2c-filter-walk:
    Regex(exp) -> $[PPaths.regexPathWalker([expStr])]
    with
      expStr := <try-p2c-exp> exp
  p2c-filter-walk:
    _ -> $[PPaths.patternsPathWalker([expStr])]
    where ?Pattern(exp) + ?Patterns(exp)
    with
      expStr := <try-p2c-exp> exp
  p2c-filter-walk:
    _ -> $[PPaths.extensionsPathWalker([expStr])]
    where ?Extension(exp) + ?Extensions(exp)
    with
      expStr := <try-p2c-exp> exp


  p2c-exp:
    ReadPath(exp) -> $[requireOutput(Read::class.java, [expStr])]
    with
      expStr := <p2c-exp> exp
      
  p2c-exp:
    ExistsPath(exp) -> $[requireOutput(Exists::class.java, [expStr])]
    with
      expStr := <p2c-exp> exp

rules // Interpolated string

  p2c-exp:
    StrLit(parts) -> $["[partsStr]"]
    with
      partsStr := <map(p2c-strpart); concat-strings> parts

  p2c-strpart:
    Str(chars) -> chars

  p2c-strpart:
    StrInterpRef(refExp) -> $[${[refExpStr]}]
    with
      refExpStr := <try-p2c-exp> refExp

  p2c-strpart:
    StrInterpExp(exp) -> $[${[expStr]}]
    with
      expStr := <try-p2c-exp> exp

rules // Interpolated path

  p2c-exp:
    PathLit(prefix, parts) -> $[resolve("[prefix][partsStr]")]
    with
      partsStr := <map(p2c-strpart); concat-strings> parts

  p2c-strpart:
    Path(chars) -> chars

  p2c-strpart:
    PathInterpRef(refExp) -> $[${[refExpStr]}]
    with
      refExpStr := <try-p2c-exp> refExp

  p2c-strpart:
    PathInterpExp(exp) -> $[${[expStr]}]
    with
      expStr := <try-p2c-exp> exp

rules // Literals

  p2c-exp: BoolLit(True())  -> $[true]
  p2c-exp: BoolLit(False()) -> $[false]
  p2c-exp: IntLit(i)        -> i
  p2c-exp: NullLit()        -> $[null]
  
  p2c-exp:
    TupleLit(exps) -> $[tuple([expsStr])]
    with
      expsStr := <map(try-p2c-exp); pipe-concat-comma> exps
    
  p2c-exp:
    ListLit(exps) -> $[list([expsStr])]
    with
      expsStr := <map(try-p2c-exp); pipe-concat-comma> exps
