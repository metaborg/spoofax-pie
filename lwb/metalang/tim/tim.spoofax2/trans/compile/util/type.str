module compile/util/type

imports

  compile/ast
  signatures/tim-sig
  signatures/tim/-
  signatures/llvm-sig
  signatures/llvm/-

rules

  get-source-type : TValueVar(_, t) -> t
  get-source-type : TValueGlobal(_, t) -> t
  get-source-type : TValueInt(_) -> INT()
  get-source-type : TValueString(_) -> STRING()
  
  get-target-type : INT() -> Int64()
  get-target-type : FUNCTION(_) -> Pointer()
  get-target-type : v -> t
  where t := <get-source-type ; get-target-type> v
  get-target-type : _ -> PointerAddrSpace("1")
  
  get-target-elem-type : CLOSURE(types) -> Struct(<map(get-target-type)> types)
  get-target-elem-type : ANONCLOSURE() -> Array("1", Pointer())
  get-target-elem-type : STRUCT(members) -> Struct(<map(?(_, <id>) ; get-target-type)> members)
  get-target-elem-type : v -> t
  where t := <get-source-type ; get-target-elem-type> v

  get-member-type : (STRUCT(members), field) -> type
  with type := <getfirst(?(field, <id>))> members
  
  get-member-index : (STRUCT(members), field) -> idx
  with idx := <get-index0(?(field, _))> members