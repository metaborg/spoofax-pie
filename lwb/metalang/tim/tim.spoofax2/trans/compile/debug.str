module compile/debug

imports

  signatures/tim-sig
  signatures/tim/-
  compile/ast

rules

  fix-invalid-ast = bottomup(try(fix-invalid-elem))

  fix-invalid-elem : TValueGlobal(x, t) -> TValueVar($[glb_[x]_[<pp-type> t]])
  
  fix-invalid-elem : TValueVar(x, t) -> TValueVar($[[x]_[<pp-type> t]])
  
  fix-invalid-elem : TExpLet([], b) -> b
  fix-invalid-elem : TExpFix([], b) -> b
  
  fix-invalid-elem : TBind(name, str) -> TBind($[glb_[name]_str], str)
  
  fix-invalid-elem : TFun(name, FUNCTION(types), args, body) -> TFun($[glb_[name]_fun], args', body)
  with
    args' := <zip; map(\(arg, type) -> $[[arg]_[<pp-type> type]] \)> (args, types)
  fix-invalid-elem : TFunOffset(name, i, FUNCTION(types), args, body) -> TFun($[glb_[name]_fun], args', body)
  with
    args' := <zip; map(\(arg, type) -> $[[arg]_[<pp-type> type]] \)> (args, types)
  
  fix-invalid-elem : TExpPrimOp(op, type, args, result, next) -> TExpPrimOp(op, args, $[[result]_[<pp-type> type]], next)
  
  pp-type : INT() -> "int"
  pp-type : STRING() -> "str"
  pp-type : FUNCTION(_) -> "fun"
  pp-type : CLOSURE() -> "clos"
  pp-type : _ -> "ptr"