module compile/explicate-types

imports

  statix/api
  signatures/tim/-
  signatures/tim-sig
  compile/ast

rules

  explicate-types : program -> program'
  with
    analysis := <stx-get-ast-analysis> program
  ; program' := <bottomup(try(preserve-annos(explicate-type(|analysis))))> program

  explicate-type(|analysis) : TExpPrimOp("print", [v], result, cont) ->
    TExpPrimOp(print, [v], result, cont)
  with
    type := <stx-get-ast-type(|analysis)> v
  ; print := <print-for-type> type
  
  explicate-type(|analysis) : t@TValueVar(x) -> TValueVar(x, type)
  with
    type := <stx-get-ast-type(|analysis)> t

  explicate-type(|analysis) : f@TFun(x, args, body) -> TFun(x, type, args, body)
  with
    type := <stx-get-ast-type(|analysis)> f

  print-for-type : INT() -> "print-int"
  print-for-type : STRING() -> "print-string"