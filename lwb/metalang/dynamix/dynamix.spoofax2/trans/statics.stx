module statics

imports
  signatures/dynamix-sig

  dynamix/statics/names
  dynamix/statics/type
  dynamix/statics/rules

rules
  projectOk : scope
  projectOk(s).

  fileOk : scope * DynamixProgram
  fileOk(globalS, DynamixProgram(modname, sections)) :- {modS}
    new modS,
    insertModuleName(modS, modname),
    declareModule(globalS, modname, modS),
    sectionsOk(globalS, modS, sections),
    // todo: this will assert only if the rules are imported, but compilation
    // right now unconditionally combines modules together (even if they are
    // not imported by any file) (#2)
    assertNoEqualRuleImplementations(modS).
    // true.
  fileOk(globalS, DynamixMultiProgram(programs)) :-
    filesOk(globalS, programs).
  filesOk maps fileOk(*, list(*))

rules
  sectionOk : scope * scope * MSection
  sectionsOk maps sectionOk(*, *, list(*))

  sectionOk(globalS, s, MSectionImports(imports)) :- importsOk(globalS, s, imports).
  sectionOk(_, s, MSectionSignature(decls)) :- signatureDeclsOk(s, decls).
  sectionOk(_, s, MSectionRules(rules)) :- ruleDeclsOk(s, rules).
  sectionOk(_, s, MSectionExample(exp)) :- typeOfExp(s, exp) == _.
  sectionOk(_, s, MSectionPrimitives(decls)) :- primitiveDeclsOk(s, decls).
  sectionOk(_, s, MSectionConstraintAnalyzer(props)) :- constraintPropertyDeclsOk(s, props).

  importOk : scope * scope * MID
  importsOk maps importOk(*, *, list(*))
  importOk(globalS, modS, import) :- {importS}
    resolveModule(globalS, import) == importS,
    modS -I-> importS.

// primitives section
rules
  primitiveDeclOk : scope * MPrimitiveDecl
  primitiveDeclsOk maps primitiveDeclOk(*, list(*))

  primitiveDeclOk(s, MPrimitiveDeclExpression(name, tys)) :- {AT}
    mTypesToMTYPEs(s, tys) == AT,
    primitiveArgumentTypesOk(AT) | error $[Primitives may only take @value or List(@value) as argument type.],
    declarePrimitive(s, name, MPRIMITIVEEXPR(AT)).
  primitiveDeclOk(s, MPrimitiveDeclStatement(name, tys)) :- {AT}
    mTypesToMTYPEs(s, tys) == AT,
    primitiveArgumentTypesOk(AT) | error $[Primitives may only take @value or List(@value) as argument type.],
    declarePrimitive(s, name, MPRIMITIVESTMT(AT)).
  primitiveDeclOk(s, MPrimitiveDeclConditional(name, tys)) :- {AT}
    mTypesToMTYPEs(s, tys) == AT,
    primitiveArgumentTypesOk(AT) | error $[Primitives may only take @value or List(@value) as argument type.],
    declarePrimitive(s, name, MPRIMITIVECOND(AT)).

  primitiveArgumentTypeOk : MTYPE
  primitiveArgumentTypesOk maps primitiveArgumentTypeOk(list(*))

  primitiveArgumentTypeOk(MTTYPE(TVALUE())).
  primitiveArgumentTypeOk(MLIST(MTTYPE(TVALUE()))).

// signatures section
rules
  signatureDeclOk : scope * MSignatureDecl
  signatureDeclsOk maps signatureDeclOk(*, list(*))

  signatureDeclOk(s, MSignatureSortsDecl(sorts)) :- defineSorts(s, sorts).
  signatureDeclOk(s, MSignatureConstructorsDecl(cons)) :- constructorDeclsOk(s, cons).

  constructorDeclOk : scope * MConstructorDecl
  constructorDeclsOk maps constructorDeclOk(*, list(*))

  constructorDeclOk(s, MConstructorDeclSingleton(consname, sortname)) :-
    declareConstructor(s, consname, SCONS([], resolveSort(s, sortname))).
  constructorDeclOk(s, MConstructorDeclInjection(from, to)) :- {S}
    resolveSort(s, to) == S,
    declareInjection(
      s,
      constructorArgumentToType(s, from),
      S
    ).
  constructorDeclOk(s, MConstructorDecl(consname, args, sortname)) :-
    declareConstructor(s, consname, SCONS(
      constructorArgumentsToTypes(s, args),
      resolveSort(s, sortname)
    )).

  defineSort : scope * UID
  defineSort(s, x) :- declareSort(s, x, SSORT(x, s)).
  defineSorts maps defineSort(*, list(*))

// constraint-analyzer section
rules
  constraintPropertyDeclOk : scope * MConstraintPropertyDecl
  constraintPropertyDeclsOk maps constraintPropertyDeclOk(*, list(*))

  constraintPropertyDeclOk(s, MConstraintPropertyDecl(name, ty)) :- {T}
    mTypeToMTYPE(s, ty) == T,
    @name.isConstraintProperty := "1", // for merging/desugaring
    try { isListOrSourceType(T) } | error $[Constraint property types must be source types (starting with ') or lists of source types.],
    declareRule(s, name, MRULE([MANYSOURCE()], MLIST(T))).

  isListOrSourceType : MTYPE
  isListOrSourceType(MSTYPE(_)).
  isListOrSourceType(MLIST(T)) :- isListOrSourceType(T).

// rules section
rules
  ruleDeclOk : scope * MRuleDecl
  ruleDeclsOk maps ruleDeclOk(*, list(*))

  ruleDeclOk(s, MRuleDeclGlobal(name, ty)) :- {T}
    mTypeToMTYPE(s, ty) == T,
    declareGlobal(s, name, T),
    assignModuleName(s, astId(name)).
  ruleDeclOk(s, MRuleDeclSignature(name, sig)) :-
    declareRule(s, name, ruleSignatureToMRULE(s, sig)),
    assignModuleName(s, astId(name)).
  ruleDeclOk(s, d@MRuleDecl(name, pats, body)) :- {as bs AT RT BT rs defId implS}
    new as, as -P-> s, new bs, bs -P-> as,
    resolveRule(s, name) == (defId, MRULE(AT, RT)),
    new implS,
    !ruleImplOf[MRuleDecl(name, desugarPatterns(pats), MExprVar("bogus"))] in implS,
    !ruleImpl[defId, implS] in s, // don't care about the body, desugar pattern once for performance
    declareZipPatterns(s, as, pats, AT) == _, // | error $[The argument pattern for [name] does not agree with its definition.],
    typeOfExp(bs, body) == BT,
    typeCoercibleTo(BT, RT) | error $[The return type of this rule, [BT], is not compatible with the declared return type, [RT].].

  ruleSignatureToMRULE : scope * MRuleSignature -> MRULE
  ruleSignatureToMRULE(s, MRuleSignature(args, ret)) = MRULE(mTypesToMTYPEs(s, args), mTypeToMTYPE(s, ret)).
  ruleSignatureToMRULE(s, MRuleZeroSignature(ret)) = MRULE([], mTypeToMTYPE(s, ret)).

// rule patterns
rules
  declareZipPatterns maps declarePattern(*, *, list(*), list(*)) = list(*)
  declarePatterns maps declarePattern(*, *, list(*), *) = list(*)

  //               s       defS    pat        expectedType    actualType
  declarePattern : scope * scope * MPattern * MTYPE        -> MTYPE
  declarePattern(_, _, MPatternWildcard(), T) = T.
  declarePattern(_, _, MPatternString(_), T) = MSTYPE(SSTRING()) :-
    typeCoercibleTo(MSTYPE(SSTRING()), T) | error $[A string pattern cannot be used to match values of type [T].].
  declarePattern(_, _, MPatternInt(_), T) = MSTYPE(SINT()) :-
    typeCoercibleTo(MSTYPE(SINT()), T) | error $[An integer pattern cannot be used to match values of type [T].].
  declarePattern(_, ds, p@MPatternVariable(name), ty) = ty :-
    declareVar(ds, name, ty),
    @p.type := ty.
  declarePattern(s, ds, MPatternList(pats), ty) = MLIST(ET) :-
    listCoercibleTo(ty, MLIST(ET)) | error $[Cannot list-pattern match on a non-list type [ty].],
    declarePatterns(s, ds, pats, ET) == _.
  declarePattern(s, ds, MPatternListCons(pats, retpat), ty) = MLIST(ET) :-
    listCoercibleTo(ty, MLIST(ET)) | error $[Cannot list-pattern match on a non-list type [ty].],
    declarePatterns(s, ds, pats, ET) == _,
    declarePattern(s, ds, retpat, ty) == _.
  declarePattern(s, ds, MPatternConstructor(name, argpats), ty) = MSTYPE(ST) :- {sn AT}
    resolveConstructor(s, name) == SCONS(AT, ST),
    declareZipPatterns(s, ds, argpats, AT) == _,
    typeCoercibleTo(MSTYPE(ST), ty) | error $[This pattern matches values of sort [ST], which is not compatible with the expected type of this pattern [ty].].
  declarePattern(s, ds, MPatternBound(name, pat), ty) = PT :-
    declarePattern(s, ds, pat, ty) == PT,
    declareVar(ds, name, PT),
    @name.type := PT.

// expressions
rules
  typeOfExp : scope * MExpr -> MTYPE
  typesOfExps maps typeOfExp(*, list(*)) = list(*)
  typeOfExp(s, e) = T :-
    typeOfExp_(s, e) == T,
    @e.type := T.

  typeOfExp_ : scope * MExpr -> MTYPE
  typeOfExp_(s, MExprVar(x)) = resolveVar(s, x).
  typeOfExp_(s, MExprGlobal(x)) = resolveGlobal(s, x).
  typeOfExp_(s, MExprQuote(t)) = typeOfQuote(s, t).
  typeOfExp_(s, MExprBlock(stmts)) = typeOfBlockStmts(s, stmts).
  typeOfExp_(s, MExprCall(ruleName, args)) = RT :- {AT}
    resolveRule(s, ruleName) == (_, MRULE(AT, RT)),
    callTypesCompatible(typesOfExps(s, args), AT).
  typeOfExp_(s, MExprLabeled0(body, labelName)) = MHOLY0() :- {s' RT}
    new s', s' -P-> s,
    declareVar(s', labelName, MTTYPE(TVALUE())),
    typeOfExp(s', body) == RT,
    try { RT == MTTYPE(TSTATEMENT()) } | error $[The body of a labeled expression must always result in a tail-call (value of type @statement).].
  typeOfExp_(s, MExprLabeled1(body, labelName)) = MHOLY1(MTTYPE(TVALUE())) :- {s' RT}
    new s', s' -P-> s,
    declareVar(s', labelName, MTTYPE(TVALUE())),
    typeOfExp(s', body) == RT,
    try { RT == MTTYPE(TSTATEMENT()) } | error $[The body of a labeled expression must always result in a tail-call (value of type @statement).].
  typeOfExp_(s, MExprSourceIntToTargetInt(v)) = MTTYPE(TVALUE()) :- {T}
    typeOfExp(s, v) == T,
    try { coercibleToInt(T) } | error $[Can only coerce strings and integers to target integers.].
  typeOfExp_(s, MExprSourceVarToTargetVar(v)) = MTTYPE(TVALUE()) :- {T}
    typeOfExp(s, v) == T,
    try { typeCoercibleTo(MSTYPE(SSTRING()), T) } | error $[Can only coerce strings to target variables.].
  typeOfExp_(s, MExprSourceStrToTargetStr(v)) = MTTYPE(TVALUE()) :- {T}
    typeOfExp(s, v) == T,
    try { typeCoercibleTo(MSTYPE(SSTRING()), T) } | error $[Can only coerce strings to target strings.].
  typeOfExp_(s, MExprFreshNamedTargetVar(_)) = MTTYPE(TVALUE()).
  typeOfExp_(s, MExprList([])) = MLIST(MAUTO(_)).
  typeOfExp_(s, MExprList(es)) = MLIST(ET) :-
    lub(typesOfExps(s, es)) == ET | error $[List literal does not have a common type.].
  typeOfExp_(s, MExprListCons(es, rest)) = MLIST(ET) :-
    lub(typesOfExps(s, es)) == ET | error $[List literal does not have a common type.],
    typeCoercibleTo(typeOfExp(s, rest), MLIST(ET)) | error $[List rest literal does not share a type with the given elements.].
  typeOfExp_(s, MExprConcatList(left, right)) = MLIST(LET) :- {RET}
    typeOfExp(s, left) == MLIST(LET) | error $[List concatenation performed on non-list value.],
    typeOfExp(s, right) == MLIST(RET) | error $[List concatenation performed on non-list value.],
    try { LET == RET } | error $[List concatenation types are not compatible with each other.].
  typeOfExp_(s, MExprContinueAt(tgt, args)) = MTTYPE(TSTATEMENT()) :-
    typeCoercibleTo(typeOfExp(s, tgt), MTTYPE(TVALUE())) | error $[Can only continue on target values.],
    typeCoercibleTo(typeOfExp(s, args), MLIST(MTTYPE(TVALUE()))) | error $[Continuation arguments must be a list of target values.].
  typeOfExp_(s, MExprCallPrimitive(name, args)) = primitiveReturnType(P) :- {AT EAT}
    typesOfExps(s, args) == AT,
    resolvePrimitive(s, name) == P,
    primitiveArgumentsType(P) == EAT | error $[Primitive [name] is not defined or not an expression or statement primitive.],
    callTypesCompatible(AT, EAT).
  typeOfExp_(s, MExprCallTypedPrimitive(name, type, args)) = primitiveReturnType(P) :- {AT EAT}
    typeCoercibleTo(typeOfExp(s, type), MTTYPE(TTYPE())),
    typesOfExps(s, args) == AT,
    resolvePrimitive(s, name) == P,
    primitiveArgumentsType(P) == EAT | error $[Primitive [name] is not defined or not an expression or statement primitive.],
    callTypesCompatible(AT, EAT).
  typeOfExp_(s, MExprConditionalPrimitive(name, args, ift, ife)) = MTTYPE(TSTATEMENT()) :- {AT IT ET EAT}
    typesOfExps(s, args) == AT,
    resolvePrimitive(s, name) == MPRIMITIVECOND(EAT) | error $[Primitive [name] is not defined or is not a conditional primitive.],
    callTypesCompatible(AT, EAT),
    typeOfExp(s, ift) == IT,
    typeOfExp(s, ife) == ET,
    try { IT == MTTYPE(TSTATEMENT()) } | error $[The true branch of a conditional expression must always result in a tail-call (value of type @statement).],
    try { ET == MTTYPE(TSTATEMENT()) } | error $[The false branch of a conditional expression must always result in a tail-call (value of type @statement).].
  typeOfExp_(s, MExprTLet(name, val, body)) = RT :-
    typeOfExp(s, name) == MTTYPE(TVALUE()) | error $[`let` name must be a @value.]@name,
    typeOfExp(s, val) == MTTYPE(TVALUE()) | error $[`let` value must be a @value.]@val,
    RT == composeTypes(MHOLY0(), typeOfExp(s, body)).
  typeOfExp_(s, MExprHole()) = MHOLY0().
  typeOfExp_(s, MExprNameOfFun(e)) = MTTYPE(TVALUE()) :-
    typeCoercibleTo(typeOfExp(s, e), MTTYPE(TFUN())) | error $[Expected @fun value as argument to nameof operator.].
  typeOfExp_(s, MExprTFix(funs)) = MHOLY0() :- {Ts}
    typesOfExps(s, funs) == Ts,
    try { validTFixMembers(Ts) } | error $[Expressions within a fix block must be of type @fun or List(@fun).].
  typeOfExp_(s, MExprConcatenateStr(left, right)) = MSTYPE(SSTRING()) :-
    typeCoercibleTo(MSTYPE(SSTRING()), typeOfExp(s, left)) | error $[The left-hand side of the `+` operator must be a source string. Runtime addition is done through primitives, not the + operator.]@left,
    typeCoercibleTo(MSTYPE(SSTRING()), typeOfExp(s, right)) | error $[The right-hand side of the `+` operator must be a source string. Runtime addition is done through primitives, not the + operator.]@right.
  typeOfExp_(s, MExprScopedGlobals(bindings, exp)) = typeOfExp(s, exp) :-
    scopedGlobalBindingsOk(s, bindings).
  typeOfExp_(s, MExprQuoteTarget(_)) = MHOLY0() :-
    try { false } | warning $[Quoted target blocks are deprecated and will be removed.].
  typeOfExp_(s, MExprTFun(name, args, body)) = MTTYPE(TFUN()) :- {s' RT}
    typeCoercibleTo(typeOfExp(s, name), MTTYPE(TVALUE())) | error $[The name of a function must be a value.],
    typeCoercibleTo(typeOfExp(s, args), MLIST(MTTYPE(TVALUE()))) | error $[The arguments of a function must be a list of values.],
    new s', s' -P-> s,
    typeOfExp(s', body) == RT,
    try { RT == MTTYPE(TSTATEMENT()) } | error $[The body of a function definition must always result in a tail-call (value of type @statement).].
  typeOfExp_(s, MExprTTypedFun(name, type, args, body)) = MTTYPE(TFUN()) :- {s' RT}
    typeCoercibleTo(typeOfExp(s, type), MTTYPE(TTYPE())),
    typeCoercibleTo(typeOfExp(s, name), MTTYPE(TVALUE())) | error $[The name of a function must be a value.],
    typeCoercibleTo(typeOfExp(s, args), MLIST(MTTYPE(TVALUE()))) | error $[The arguments of a function must be a list of values.],
    new s', s' -P-> s,
    typeOfExp(s', body) == RT,
    try { RT == MTTYPE(TSTATEMENT()) } | error $[The body of a function definition must always result in a tail-call (value of type @statement).].

  // Target type usages
  typeOfExp_(s, MExprTTypes(defs)) = MHOLY0() :-
    typeDefsOk(s, defs).

  typeDefOk : scope * MTypeDef
  typeDefsOk maps typeDefOk(*, list(*))
  typeDefOk(s, MTypeDef(target, value)) :-
    typeOfExp(s, target) == MSTYPE(SSTRING()),
    typeOfExp(s, value) == MTTYPE(TTYPE()).

  typeOfExp_(s, MExprTTypeInt()) = MTTYPE(TTYPE()).
  typeOfExp_(s, MExprTTypeString()) = MTTYPE(TTYPE()).
  typeOfExp_(s, MExprTTypeFunction(argTypes)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, argTypes), MLIST(MTTYPE(TTYPE()))).
  typeOfExp_(s, MExprTTypeRef(T)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, T), MTTYPE(TTYPE())).
  typeOfExp_(s, MExprTTypeArray(T)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, T), MTTYPE(TTYPE())).
  typeOfExp_(s, MExprTTypeRecord(T)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, T), MTTYPE(TTYPE())).
  typeOfExp_(s, MExprTTypeAny()) = MTTYPE(TTYPE()).
  typeOfExp_(s, MExprTTypeStruct(fields)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, fields), MLIST(MTTYPE(TSTRUCTMEMBER()))).
  typeOfExp_(s, MExprTStructMember(name, type)) = MTTYPE(TSTRUCTMEMBER()) :-
    typeCoercibleTo(typeOfExp(s, name), MSTYPE(SSTRING())),
    typeCoercibleTo(typeOfExp(s, type), MTTYPE(TTYPE())).
  typeOfExp_(s, MExprTTypeNamed(name)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, name), MSTYPE(SSTRING())).
  typeOfExp_(s, MExprTTypeSymbol(name)) = MTTYPE(TTYPE()) :-
    typeCoercibleTo(typeOfExp(s, name), MSTYPE(SSTRING())).
  typeOfExp_(s, MExprTTypeWildcard()) = MTTYPE(TTYPE()).

  coercibleToInt : MTYPE
  coercibleToInt(MSTYPE(SSTRING())).
  coercibleToInt(MSTYPE(SINT())).
  coercibleToInt(T) :- typeCoercibleTo(MSTYPE(SSTRING()), T).

  validTFixMember : MTYPE
  validTFixMembers maps validTFixMember(list(*))
  validTFixMember(MLIST(MTTYPE(TFUN()))). // List(@fun)
  validTFixMember(MTTYPE(TFUN())). // @fun

  scopedGlobalBindingOk : scope * MScopedGlobalBinding
  scopedGlobalBindingsOk maps scopedGlobalBindingOk(*, list(*))
  scopedGlobalBindingOk(s, MScopedGlobalBinding(name, exp)) :- {T GT}
    typeOfExp(s, exp) == T,
    resolveGlobal(s, name) == GT,
    typeCoercibleTo(T, GT) | error $[The type of the expression ([T]) is not compatible with the declared type of `[name]`, [GT].].

  primitiveArgumentsType : MPRIMITIVE -> list(MTYPE)
  primitiveArgumentsType(MPRIMITIVEEXPR(A)) = A.
  primitiveArgumentsType(MPRIMITIVESTMT(A)) = A.

  primitiveReturnType : MPRIMITIVE -> MTYPE
  primitiveReturnType(MPRIMITIVEEXPR(_)) = MHOLY1(MTTYPE(TVALUE())).
  primitiveReturnType(MPRIMITIVESTMT(_)) = MTTYPE(TSTATEMENT()).

  callTypesCompatible : list(MTYPE) * list(MTYPE)
  callTypesCompatible([], []).
  callTypesCompatible(_, []) :- false | error $[Too many arguments supplied to this call.].
  callTypesCompatible([], _) :- false | error $[Not enough arguments supplied to this call.].
  callTypesCompatible([A|ar], [B|br]) :-
    typeCoercibleTo(A, B) | error $[The given parameter type [A] is not compatible with the expected parameter type [B].],
    callTypesCompatible(ar, br).

// quoted expressions
rules
  typeOfQuote : scope * STerm -> MTYPE
  typeOfQuote_ : scope * STerm -> MTYPE
  typesOfQuotes maps typeOfQuote(*, list(*)) = list(*)

  typeOfQuote(s, e) = T :-
    typeOfQuote_(s, e) == T,
    @e.type := T.

  typeOfQuote_(_, STermInt(_)) = MSTYPE(SINT()).
  typeOfQuote_(_, STermString(_)) = MSTYPE(SSTRING()).
  typeOfQuote_(_, STermList([])) = MLIST(MAUTO(_)).
  typeOfQuote_(s, STermList(els)) = MLIST(lub(typesOfQuotes(s, els))).
  typeOfQuote_(s, STermApp(name, args)) = MSTYPE(ST) :- {AT}
    resolveConstructor(s, name) == SCONS(AT, ST),
    quoteTypesCompatible(s, args, AT) | error $[Mismatched number of arguments.].

  quoteTypeCompatible : scope * STerm * MTYPE
  quoteTypesCompatible maps quoteTypeCompatible(*, list(*), list(*))

  quoteTypeCompatible(s, term, T) :- {TT}
    typeOfQuote(s, term) == TT,
    typeCoercibleTo(TT, T) | error $[The type [TT] is not compatible with the expected type [T].].

rules // statements and composition
  typeOfBlockStmts : scope * list(MStatement) -> MTYPE
  typeOfBlockStmts(s, []) = _ :- false | error $[A block must contain at least one statement.].
  typeOfBlockStmts(s, [stmt]) = typeOfBlockStmt(s, stmt).
  typeOfBlockStmts(s, [stmt|rest@[_|_]]) = composeTypes(ST, RT) :-
    typeOfBlockStmt(s, stmt) == ST,
    typeOfBlockStmts(s, rest) == RT.

  typeOfBlockStmt : scope * MStatement -> MTYPE
  typeOfBlockStmt(s, MStatementExpr(e)) = typeOfExp(s, e).
  typeOfBlockStmt(s, MStatementAssign(name, e)) = T :-
    typeOfExp(s, e) == T,
    performAssignment(s, name, T).

  performAssignment : scope * string * MTYPE
  performAssignment(s, n, MHOLY1(x)) :- declareVar(s, n, x).
  performAssignment(s, n, x@MTTYPE(TVALUE())) :- declareVar(s, n, x).
  performAssignment(s, n, x@MTTYPE(TFUN())) :- declareVar(s, n, x).
  performAssignment(s, n, x@MLIST(_)) :- declareVar(s, n, x).
  performAssignment(s, n, x@MSTYPE(_)) :- declareVar(s, n, x).
  performAssignment(s, n, x) :-
    try { false } | error $[Cannot assign variables of type [x] since it does not have a concrete value.],
    false.
