module dynamix/api

imports
  compilation/compile
  evaluation/eval
  
rules
  // compile the program by applying the input term to the qualified
  // rule with the given name
  dx-compile-program(|rule) = dx-compile-program(id|rule)
  
  // compile the program by applying the input term to the qualified
  // rule with the given name, but transform the input term using
  // the given stratego strategy first
  dx-compile-program(pre-compile|rule): input -> result
    with processed := <pre-compile> input
       ; spec := <dx--compiled-spec>
       ; result := <dx--eval-input-term> (spec, rule, processed)
       
rules
  dx--compiled-spec =
    language-resources(dx--compiled-spec-location, ![]);
    debug(!"Compiled spec: ");
    ?[(_, <id>)]

  dx--compiled-spec-location: _ -> $[[srcgen]/dynamix.merged.aterm]
    with srcgen := <project-srcgen-dir> "dynamix"