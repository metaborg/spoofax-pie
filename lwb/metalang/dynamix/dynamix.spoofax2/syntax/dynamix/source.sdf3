module dynamix/source

imports
  dynamix/common
  
lexical sorts SConstructorName
lexical syntax
  SConstructorName = [A-Z] [a-zA-Z0-9\_\-]*
  
lexical restrictions
  SConstructorName -/- [a-zA-Z0-9\_\-]
  
// source language (modeled after ATerms)
context-free sorts STerm
context-free syntax
  STerm.STermInt = <<INT>>
  STerm.STermString = <<STRING>>
  STerm.STermApp = <<SConstructorName>(<{STerm ", "}*>)>
  STerm.STermList = <[<{STerm ", "}*>]>
  STerm.STermTuple = <(<{STerm ", "}+>)>
  
// source language types (modeled after statix/stratego syntax)
context-free sorts SType SConstructorType
context-free syntax
  SType.STypeNamed = <<UID>>
  SType.STypeInt = <int>
  SType.STypeString = <string>
  SType.STypeList = <list(<SType>)>
  SType.STypeTuple = <(<{SType " * "}+>)>
  
  SConstructorType.SConstructorTypeSingleton = <<UID>>
  SConstructorType.SConstructorTypeParams = <<{SType " * "}+> -\> <UID>>