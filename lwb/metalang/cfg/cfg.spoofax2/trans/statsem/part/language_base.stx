module statsem/part/language_base

imports

  statsem/part
  statsem/expr
  statsem/util

  signatures/part/language_base-sig

rules // Parser section and options

  partOk(s, t@ParserSection(options)) :-
    parserOptionsOk(s, options),
    hasDefaultStartSymbol(options) == TRUE() | error $['default-start-symbol' option has not been set]@t.


  parserOptionOk : scope * ParserOption
  parserOptionsOk maps parserOptionOk(*, list(*))

  parserOptionOk(s, DefaultStartSymbol(e)) :-
    typeOfExpr(s, e) == SORT() | error $[Expected sort]@e.
  parserOptionOk(s, ParserVariant(variant)) :-
    parserVariantOk(s, variant).


  parserVariantOk : scope * ParserVariant

  parserVariantOk(s, Jsglr1()).
  parserVariantOk(s, Jsglr2(options)) :-
    jsglr2OptionsOk(s, options).


  jsglr2OptionOk : scope * Jsglr2Option
  jsglr2OptionsOk maps jsglr2OptionOk(*, list(*))

  jsglr2OptionOk(s, Jsglr2Preset(_)).


  hasDefaultStartSymbol: list(ParserOption) -> BOOL
  hasDefaultStartSymbol([DefaultStartSymbol(_)|_]) = TRUE().
  hasDefaultStartSymbol([_|s]) = hasDefaultStartSymbol(s).
  hasDefaultStartSymbol([]) = FALSE().

rules // Comment symbols and options

  partOk(s, t@CommentSymbolSection(options)) :-
    commentSymbolOptionsOk(s, options).

  commentSymbolOptionOk : scope * CommentSymbolOption
  commentSymbolOptionsOk maps commentSymbolOptionOk(*, list(*))

  commentSymbolOptionOk(s, LineComment(e)) :-
    typeOfExpr(s, e) == STRING() | error $[Expected string]@e.
  commentSymbolOptionOk(s, BlockComment(e1, e2)) :-
    typeOfExpr(s, e1) == STRING() | error $[Expected string]@e1,
    typeOfExpr(s, e2) == STRING() | error $[Expected string]@e2.

rules // Bracket symbols and options

  partOk(s, t@BracketSymbolSection(options)) :-
    bracketSymbolOptionsOk(s, options).

  bracketSymbolOptionOk : scope * BracketSymbolOption
  bracketSymbolOptionsOk maps bracketSymbolOptionOk(*, list(*))

  bracketSymbolOptionOk(s, Bracket(e1, e2)) :-
    typeOfExpr(s, e1) == CHAR() | error $[Expected symbol (e.g., '\[')]@e1,
    typeOfExpr(s, e2) == CHAR() | error $[Expected symbol (e.g., '\]')]@e2.

rules // Styler section and options

  partOk(s, StylerSection(options)) :-
    stylerOptionsOk(s, options).

  stylerOptionOk : scope * StylerOption
  stylerOptionsOk maps stylerOptionOk(*, list(*))

rules // Constraint analyzer section and options

  partOk(s, ConstraintAnalyzerSection(options)) :-
    constraintAnalyzerOptionsOk(s, options).

  constraintAnalyzerOptionOk : scope * ConstraintAnalyzerOption
  constraintAnalyzerOptionsOk maps constraintAnalyzerOptionOk(*, list(*))

  constraintAnalyzerOptionOk(s, ConstraintAnalyzerEnableNaBL2(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerEnableStatix(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerMultiFile(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerStrategoStrategy(e)) :-
    typeOfExpr(s, e) == STRATEGY() | error $[Expected Stratego strategy identifier]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerStatixSolverMode(_)).

  constraintAnalyzerOptionOk(s, ConstraintAnalyzerDefaultStatixMessageStacktraceLength(e)) :-
    typeOfExpr(s, e) == UINT() | error $[Expected unsigned integer]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerDefaultStatixMessageTermDepth(e)) :-
    typeOfExpr(s, e) == UINT() | error $[Expected unsigned integer]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerDefaultStatixTestLogLevel(e)) :-
    typeOfExpr(s, e) == STRING() | error $[Expected string]@e.
  constraintAnalyzerOptionOk(s, ConstraintAnalyzerDefaultStatixSuppressCascadingErrors(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.

rules // Multilang analyzer section and options

  partOk(s, MultilangAnalyzerSection(options)) :-
    multilangAnalyzerOptionsOk(s, options).

  multilangAnalyzerOptionOk : scope * MultilangAnalyzerOption
  multilangAnalyzerOptionsOk maps multilangAnalyzerOptionOk(*, list(*))

rules // Stratego runtime section and options

  partOk(s, StrategoRuntimeSection(options)) :-
    strategoRuntimeOptionsOk(s, options).

  strategoRuntimeOptionOk : scope * StrategoRuntimeOption
  strategoRuntimeOptionsOk maps strategoRuntimeOptionOk(*, list(*))

  strategoRuntimeOptionOk(s, StrategoRuntimeStrategyPackageId(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java package identifier]@e.
  strategoRuntimeOptionOk(s, StrategoRuntimeInteropRegistererByReflection(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java type identifier]@e.
  strategoRuntimeOptionOk(s, StrategoRuntimeAddSpoofax2Primitives(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.
  strategoRuntimeOptionOk(s, StrategoRuntimeAddNaBL2Primitives(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.
  strategoRuntimeOptionOk(s, StrategoRuntimeAddStatixPrimitives(e)) :-
    typeOfExpr(s, e) == BOOL() | error $[Expected boolean]@e.

  strategoRuntimeOptionOk(s, StrategoRuntime_WithPrimitiveLibrary(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java type identifier]@e.
  strategoRuntimeOptionOk(s, StrategoRuntime_WithInteropRegisterer(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java type identifier]@e.

  strategoRuntimeOptionOk(s, StrategoRuntime_ClassKind(_)).
  strategoRuntimeOptionOk(s, StrategoRuntime_BaseStrategoRuntimeBuilderFactory(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java type identifier]@e.
  strategoRuntimeOptionOk(s, StrategoRuntime_ExtendStrategoRuntimeBuilderFactory(e)) :-
    typeOfExpr(s, e) == JAVA_ID() | error $[Expected Java type identifier]@e.


rules // Tego runtime section and options

  partOk(s, TegoRuntimeSection(options)) :-
    tegoRuntimeOptionsOk(s, options).

  tegoRuntimeOptionOk : scope * TegoRuntimeOption
  tegoRuntimeOptionsOk maps tegoRuntimeOptionOk(*, list(*))

rules // Code completion section and options

  partOk(s, CodeCompletionSection(options)) :-
    codeCompletionOptionsOk(s, options).

  codeCompletionOptionOk : scope * CodeCompletionOption
  codeCompletionOptionsOk maps codeCompletionOptionOk(*, list(*))
