module part/language_adapter

imports

  part
  expr

context-free sorts

  AdapterOption

context-free syntax

  Part.AdapterSection = <adapter {
  <{AdapterOption "\n"}*>
}>
  AdapterOption.BaseResourcesComponent = <base-resources-component = <Expr>>
  AdapterOption.ExtendResourcesComponent = <extend-resources-component = <Expr>>
  AdapterOption.BaseResourcesModule = <base-resources-module = <Expr>>
  AdapterOption.ExtendResourcesModule = <extend-resources-module = <Expr>>
  AdapterOption.AdditionalResourcesModule = <additional-resources-module = <Expr>>

  AdapterOption.BaseComponent = <base-component = <Expr>>
  AdapterOption.ExtendComponent = <extend-component = <Expr>>
  AdapterOption.BaseModule = <base-module = <Expr>>
  AdapterOption.ExtendModule = <extend-module = <Expr>>
  AdapterOption.AdditionalModule = <additional-module = <Expr>>

  AdapterOption.BaseInstance = <base-instance = <Expr>>
  AdapterOption.ExtendInstance = <extend-instance = <Expr>>
  AdapterOption.BaseParticipant = <base-participant = <Expr>>
  AdapterOption.ExtendParticipant = <extend-participant = <Expr>>

context-free syntax

  Part.MainMenu = <main-menu [
  <{Expr "\n"}*>
]>
  Part.ResourceContextMenu = <resource-context-menu [
  <{Expr "\n"}*>
]>
  Part.EditorContextMenu = <editor-context-menu [
  <{Expr "\n"}*>
]>

lexical syntax

  Keyword = "main-menu"
  Keyword = "resource-context-menu"
  Keyword = "editor-context-menu"

context-free sorts

  ReferenceResolutionOption
  ReferenceResolutionVariant

context-free syntax

  Part.ReferenceResolutionSection = <reference-resolution {
  <{ReferenceResolutionOption "\n"}*>
}>

  ReferenceResolutionOption.ReferenceResolutionVariant = <variant = <ReferenceResolutionVariant>>
  ReferenceResolutionVariant.ReferenceResolutionStrategoVariant = <stratego {
    strategy = <Expr>
  }>

context-free sorts

  HoverOption
  HoverVariant

context-free syntax

  Part.HoverSection = <hover {
  <{HoverOption "\n"}*>
}>

  HoverOption.HoverVariant = <variant = <HoverVariant>>
  HoverVariant.HoverStrategoVariant = <stratego {
    strategy = <Expr>
  }>

context-free syntax

  Part.DependOnRv32Im = <depend-on-rv32im = <Expr>>
