module mini-str/signatures/constructors

imports
  abstract-sig/types
  abstract-sig/sorts
  abstract-sig/constructors

  signatures/ministr-sig
  mini-str/signatures/sorts

rules

  conssOk maps consOk(*, list(*))

  consOk: scope * ConstructorDef

  consOk(s, NoArgs(cn, sn)) :- {T}
    sortOfSort(s, sn) == T,
    declareCons(s, T, cn, []).

  consOk(s, WithArgs(cn, p, sn)) :- {T T1}
    sortOfSort(s, sn) == T,
    sortsOfParams(s, p) == T1,
    declareCons(s, T, cn, T1).

rules

  sortsOfParams maps sortOfParam(*, list(*)) = list(*)

  sortOfParam: scope * ArgSort -> TYPE

  sortOfParam(s, Sort(n)) = SINGLE(sortOfSort(s, n)).
  sortOfParam(s, SOpt(n))  = OPT(sortOfSort(s, n)).
  sortOfParam(s, SIter(n)) = ITER(sortOfSort(s, n)).
  sortOfParam(s, SStar(n)) = STAR(sortOfSort(s, n)).
