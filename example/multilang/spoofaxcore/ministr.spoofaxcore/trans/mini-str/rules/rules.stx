module mini-str/rules/rules

imports
  abstract-sig/types
  abstract-sig/sorts

  signatures/ministr-sig
  mini-str/rules/variables
  mini-str/rules/resolution
  mini-str/rules/patterns

rules

  rulesOk: scope * scope * scope * list(RuleDef)
  rulesOk(_, s_out, s_out, []).
  rulesOk(s_root, s_seq, s_out, [h | t]) :- {s}
    new s, s -S-> s_seq,
    ruleOk(s_root, s_seq, h),
    rulesOk(s_root, s, s_out, t).

  ruleOk: scope * scope * RuleDef
  ruleOk(s_root, s_seq, RewriteRuleDef(n, m, b, w)) :-
    {s_match s_build s_with T1 T2}
      new s_match s_build s_with,
      s_match -P-> s_root,
      s_build -P-> s_with,
      declareRule(s_root, s_seq, n, T1, T2),
      typeOfBuild(s_build, b) == T2,
      withOk(s_match, s_with, w),
      typeOfMatch(s_match, m) == T1.

rules

  withOk: scope * scope * list(With)

  withOk(s_match, s_build, []) :-
    s_build -P-> s_match.

  withOk(s_match, s_build, [With(str)]) :-
    strategyOk(s_match, s_build, str).

rules

  strategyOk : scope * scope * Strategy

  strategyOk (s_match, s_decl, Assign(n, p)) :- {T}
    s_decl -P-> s_match,
    typeOfBuild(s_match, p) == T,
    declareVar(s_decl, n, T).

  strategyOk(s_match, s_build, Seq(str1, str2)) :- {s_int}
    new s_int,
    strategyOk(s_match, s_int, str1),
    strategyOk(s_int, s_build, str2).
