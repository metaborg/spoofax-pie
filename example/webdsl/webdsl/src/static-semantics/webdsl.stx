module static-semantics/webdsl

imports
  signatures/start-sig

  static-semantics/actions/built-ins
  static-semantics/entities/built-ins
  static-semantics/types/built-ins
  static-semantics/ui/built-ins

  static-semantics/entities/annotations

  static-semantics/webdsl-ac
  static-semantics/webdsl-actions
  static-semantics/webdsl-entities
  static-semantics/webdsl-expand
  static-semantics/webdsl-hql
  static-semantics/webdsl-modules
  static-semantics/webdsl-native
  static-semantics/webdsl-regex
  static-semantics/webdsl-search
  static-semantics/webdsl-services
  static-semantics/webdsl-types
  static-semantics/webdsl-ui

signature

  sorts
    TYPE // semantic type
    BOOL // used as return values of functional constraints

  constructors
    TRUE : BOOL
    FALSE : BOOL

  name-resolution
    labels
      P       // parent edge label for consecutive statements
      F       // function edge label for embedding functions, templates and pages in a scope
      EXTEND  // extend edge label for extending entities
      INHERIT // inherit edge label for subclasses
      DEF     // definition edge label for embedding top-level definitions in the global scope
      IMPORT  // import edge label for importing another module
      GLOBAL  // global edge label for edges from global scope to other modules

  relations
    mod : list(string) * scope

    type : string * scope

    extendentity : string * scope

    page     : string * scope
    template : string * scope
    ac       : string * scope

    function : string * scope

    var      : string * scope

    typeOf : TYPE

rules // module declaration and resolving

  declareMod : scope * list(string) * scope
  declareMod(s, m, s_mod) :-
    !mod[m, s_mod] in s,
    resolveMod(s, m) == [_] | error $[Module [m] is already declared].

  resolveMod : scope * list(string) -> list((path * (list(string) * scope)))
  resolveMod(s, m) = ss :-
    query mod filter e
              and { m' :- m' == (m, _) }
              in s |-> ss.

  resolveModWildcard : scope * list(string) -> list((path * (list(string) * scope)))
  resolveModWildcard(s, names) = /*modsOf(*/ ss /*)*/ :-
    query mod filter e
              and { m' :- isPrefix(names, m') }
              in s |-> ss.

  isPrefix : list(string) * (list(string) * scope)
  isPrefix(names, (names', _)) :- isPrefix_internal(names, names').

  isPrefix_internal : list(string) * list(string)
  isPrefix_internal([], _).
  isPrefix_internal([x | xs], [x | ys]) :- isPrefix_internal(xs, ys).
  isPrefix_internal(_, _) :- false.

rules // type declaration and resolving

  declareType : scope * string * TYPE
  declareType(s, name, t) :-
    !type[name, withType(t)] in s,
    resolveType(s, name) == [(_, (_, t))] | error $[Type [name] is defined multiple times] @name.

  resolveType : scope * string -> list((path * (string * TYPE)))
  resolveType(s, name) = typesOf(ts) :-
    query type filter P* F* ((EXTEND? INHERIT*) | (DEF? IMPORT?))
               and { e' :- e' == (name, _) }
               in s |-> ts.

  withType : TYPE -> scope
  withType(t) = s :-
    new s, !typeOf[t] in s.

  typesOf maps typeOf(list(*)) = list(*)
  typeOf : (path * (string * scope)) -> (path * (string * TYPE))
  typeOf((p, (x, s))) = (p, (x, t)) :-
    query typeOf filter e in s |-> [(_, t)].

rules // extend entity declaration

  declareExtendEntity : scope * string * scope
  declareExtendEntity(s, e, s_extend) :-
    !extendentity[e, s_extend] in s.

  resolveExtendEntity : scope * string -> list((path * (string * scope)))
  resolveExtendEntity(s, e) = ps :-
    query extendentity filter P* F* ((EXTEND? INHERIT*) | (DEF? IMPORT? GLOBAL?))
                       and { e' :- e' == (e, _) }
                       min $ < P, $ < F, $ < EXTEND, $ < INHERIT, $ < DEF, $ < IMPORT, $ < GLOBAL,
                           P < F, P < EXTEND, P < INHERIT, P < DEF, P < IMPORT, P < GLOBAL,
                           F < EXTEND, F < INHERIT, F < DEF, F < IMPORT, F < GLOBAL,
                           EXTEND < INHERIT,
                           DEF < IMPORT, DEF < GLOBAL,
                           IMPORT < GLOBAL
                       and { (entity, entity_scope), (entity, entity_scope) }
                       in s |-> ps.

rules // ui declaration and resolving

  declareTemplate : scope * string * list(TYPE)
  declareTemplate(s, t, ts) :- declareTemplate_internal(s, t, ts, FALSE()).

  declareAjaxTemplate : scope * string * list(TYPE)
  declareAjaxTemplate(s, t, ts) :- declareTemplate_internal(s, t, ts, TRUE()).

  declareTemplate_internal : scope * string * list(TYPE) * BOOL
  declareTemplate_internal(s, t, ts, ajax) :-
    !template[t, withType(TEMPLATE(ts, ajax))] in s,
    noClashingPageDefinitions(s, ts, t),
    filterTemplateResultsArgs(resolveTemplate(s, t), ts) == [_] | error $[Template [t] with argument types [ts] is already defined] @t.

  // in the compiler, templates without arguments can clash with page definitions
  noClashingPageDefinitions : scope * list(TYPE) * string
  noClashingPageDefinitions(_, _, _).
  noClashingPageDefinitions(s, [], t) :- resolvePage(s, t) == [] | error $[A page with name [t] is defined, this is only allowed when this template has at least one argument] @t.

  declarePage : scope * string * list(TYPE)
  declarePage(s, p, ts) :-
    !page[p, withType(PAGE(ts))] in s,
    noClashingTemplateDefinitions(s, p),
    resolvePage(s, p) == [_] | error $[Page [p] is already defined] @p.

  noClashingTemplateDefinitions : scope * string
    noClashingTemplateDefinitions_helper maps noClashingTemplateDefinitions_internal(*, list(*))
    noClashingTemplateDefinitions_internal : string * (path * (string * TYPE))

  noClashingTemplateDefinitions(s, p) :- noClashingTemplateDefinitions_helper(p, resolveTemplate(s, p)).
    noClashingTemplateDefinitions_internal(p, (_, (_, TEMPLATE([], _)))) :- false | error $[A template with name [p] is defined, this is only allowed when that template has at least one argument] @p.
    noClashingTemplateDefinitions_internal(_, _).

  resolveTemplate : scope * string -> list((path * (string * TYPE)))
  resolveTemplate(s, t) = resolveTemplate_internal(s, t, FALSE()).

  resolveAjaxTemplate : scope * string -> list((path * (string * TYPE)))
  resolveAjaxTemplate(s, t) = resolveTemplate_internal(s, t, TRUE()).

  resolveTemplate_internal : scope * string * BOOL -> list((path * (string * TYPE)))
  resolveTemplate_internal(s, t, FALSE()) = typesOf(ps) :-
    query template filter P* F* ((EXTEND? (INHERIT EXTEND?)*) | (DEF? IMPORT?))
                   and { t' :- t' == (t, _) }
                   in s |-> ps.

  resolveTemplate_internal(s, t, TRUE()) = filterTemplateResultsAjax(resolveTemplate_internal(s, t, FALSE()), TRUE()).

  resolveTemplateNoAjax : scope * string -> list((path * (string * TYPE)))
  resolveTemplateNoAjax(s, t) = filterTemplateResultsAjax(resolveTemplate_internal(s, t, FALSE()), FALSE()).

  filterTemplateResultsAjax : list((path * (string * TYPE))) * BOOL -> list((path * (string * TYPE)))
  filterTemplateResultsAjax([], _) = [].
  filterTemplateResultsAjax([t@(_, (_, TEMPLATE(_, b))) | ts], b) = [t | filterTemplateResultsAjax(ts, b)].
  filterTemplateResultsAjax([_ | ts], b) = filterTemplateResultsAjax(ts, b).

  filterTemplateResultsArgs : list((path * (string * TYPE))) * list(TYPE) -> list((path * (string * TYPE)))
  filterTemplateResultsArgs([], _) = [].
  filterTemplateResultsArgs([t@(_, (_, TEMPLATE(args, _))) | ts], args) = [t | filterTemplateResultsArgs(ts, args)].
  filterTemplateResultsArgs([_ | ts], args) = filterTemplateResultsArgs(ts, args).

  resolvePage : scope * string -> list((path * (string * TYPE)))
  resolvePage(s, p) = typesOf(ps) :-
    query page filter P* F* ((EXTEND? (INHERIT EXTEND?)*) | (DEF? IMPORT?))
               and { p' :- p' == (p, _) }
               in s |-> ps.

rules // ac rules

  declarePointcut : scope * string * list(TYPE)
  declarePointcut(s, p, ts) :-
    !ac[p, withType(POINTCUT(p, ts))] in s,
    resolvePointcut(s, p) == [_] | error $[Pointcut [p] is defined multiple times] @p.

  resolvePointcut : scope * string -> list((path * (string * TYPE)))
  resolvePointcut(s, p) = typesOf(ps) :-
    query ac filter P* F* ((EXTEND? (INHERIT EXTEND?)*) | (DEF? IMPORT?))
             and { p' :- p' == (p, _) }
             in s |-> ps.

rules // function declaration and resolving

  declareFunction : scope * string * list(TYPE) * TYPE
  declareFunction(s, f, args, return) :- declareFunction_internal(s, f, args, return, FALSE()).

  declareStaticFunction : scope * string * list(TYPE) * TYPE
  declareStaticFunction(s, f, args, return) :- declareFunction_internal(s, f, args, return, TRUE()).

  declareFunction_internal : scope * string * list(TYPE) * TYPE * BOOL
  declareFunction_internal(s, f, args, return, static) :- { s_type }
    s_type == withType(FUNCTION(args, return, static)),
    !function[f, s_type] in s.

  resolveFunction : scope * string -> list((path * (string * TYPE)))
  resolveFunction(s, f) = filterFunctionResults(typesOf(ps), FALSE()) :-
    query function filter P* F* ((EXTEND? (INHERIT EXTEND?)*) | (DEF? IMPORT?))
                   and { f' :- f' == (f, _) }
                   in s |-> ps.

  resolveEntityFunction : scope * string -> list((path * (string * TYPE)))
  resolveEntityFunction(s, x) = filterDuplicateTypeResults(filterFunctionResults(typesOf(ps), FALSE())) :-
    query function filter EXTEND? (INHERIT EXTEND?)*
                   and { x' :- x' == (x, _) }
                   in s |-> ps.

  resolveStaticEntityFunction : scope * string -> list((path * (string * TYPE)))
  resolveStaticEntityFunction(s, x) = filterDuplicateTypeResults(filterFunctionResults(typesOf(ps), TRUE())) :-
    query function filter EXTEND? (INHERIT EXTEND?)*
                   and { x' :- x' == (x, _) }
                   in s |-> ps.

  // last BOOL arg indiciates if only static function should be returned (TRUE()) or all functions (FALSE())
  filterFunctionResults : list((path * (string * TYPE))) * BOOL -> list((path * (string * TYPE)))
  filterFunctionResults([], _) = [].
  filterFunctionResults([f@(_, (_, FUNCTION(_, _, TRUE()))) | fs], b@TRUE()) = [f | filterFunctionResults(fs, b)].
  filterFunctionResults([f@(_, (_, FUNCTION(_, _, _))) | fs], b@FALSE()) = [f | filterFunctionResults(fs, b)].
  filterFunctionResults([_ | fs], b) = filterFunctionResults(fs, b).

  filterDuplicateTypeResults : list((path * (string * TYPE))) -> list((path * (string * TYPE)))
  filterDuplicateTypeResults(rs) = filterDistinctTypeResultsZipped(zipTypeResultsWithDistinctBool(rs, [])).

  zipTypeResultsWithDistinctBool : list((path * (string * TYPE))) * list(TYPE) -> list(((path * (string * TYPE)) * BOOL))
  zipTypeResultsWithDistinctBool([], _) = [].
  zipTypeResultsWithDistinctBool([r@(_, (_, t)) | rs], ts) = [(r, notB(typeInTypeListB(t, ts))) | zipTypeResultsWithDistinctBool(rs, [t | ts])].

  typeInTypeListB : TYPE * list(TYPE) -> BOOL
  typeInTypeListB(_, []) = FALSE().
  typeInTypeListB(t, [t | _]) = TRUE().
  typeInTypeListB(t, [_ | ts]) = typeInTypeListB(t, ts).

  filterDistinctTypeResultsZipped : list(((path * (string * TYPE)) * BOOL)) -> list((path * (string * TYPE)))
  filterDistinctTypeResultsZipped([]) = [].
  filterDistinctTypeResultsZipped([(r, TRUE()) | rs]) = [r | filterDistinctTypeResultsZipped(rs)].
  filterDistinctTypeResultsZipped([_ | rs]) = filterDistinctTypeResultsZipped(rs).

rules // action and placeholder declaration and resolving

  declareAction : scope * string * list(TYPE)
  declareAction(s, a, ts) :-
    !function[a, withType(ACTION(ts))] in s,
    resolveAction(s, a) == [(_, (_, ACTION(ts)))] | error $[Action [a] is defined multiple times] @a.

  resolveAction : scope * string -> list((path * (string * TYPE)))
  resolveAction(s, a) = filterActionResults(typesOf(as)) :-
    query function filter P*
                   and { a' :- a' == (a, _) }
                   in s |-> as.

  declarePlaceholder : scope * string
  declarePlaceholder(s, ph) :-
    !var[ph, withType(placeholder(s))] in s,
    noDuplicateVarDefs(s, ph) | error $[Variable [ph] is defined multiple times] @ph.

  resolvePlaceholder : scope * string -> list((path * (string * TYPE)))
  resolvePlaceholder(s, ph) = result :- { ph_scopes phs }
    query var filter P*
              and { ph' :- ph' == (ph, _) }
              in s |-> ph_scopes,
    phs == typesOf(ph_scopes),
    result == filterPlaceholderResults(phs).

  filterActionResults : list((path * (string * TYPE))) -> list((path * (string * TYPE)))
  filterActionResults([]) = [].
  filterActionResults([a@(_, (_, ACTION(_))) | as]) = [a | filterActionResults(as)].
  filterActionResults([_ | as]) = filterActionResults(as).

  filterPlaceholderResults : list((path * (string * TYPE))) -> list((path * (string * TYPE)))
  filterPlaceholderResults([]) = [].
  filterPlaceholderResults([ph@(_, (_, BUILTINTYPE("Placeholder", _))) | phs]) = [ph | filterPlaceholderResults(phs)].
  filterPlaceholderResults([_ | phs]) = filterPlaceholderResults(phs).

rules // variable declaration and resolving

  declareVar : scope * string * TYPE
  declareVar(s, x, t) :-
    !var[x, withType(t)] in s,
    noDuplicateVarDefs(s, x).

  resolveVar : scope * string -> list((path * (string * TYPE)))
  resolveVar(s, x) = typesOf(ps) :-
    query var filter P* F* ((EXTEND? (INHERIT EXTEND?)*) | (DEF? IMPORT?))
              and { x' :- x' == (x, _) }
              min $ < P, P < F, F < EXTEND, EXTEND < INHERIT, INHERIT < DEF, DEF < IMPORT
              and true
              in s |-> ps.

  resolveProperty : scope * string -> list((path * (string * TYPE)))
  resolveProperty(s_entity, x) = typesOf(ps) :-
    query var filter EXTEND? (INHERIT EXTEND?)*
              and { x' :- x' == (x, _) }
              in s_entity |-> ps.

  resolveMutableProperty : scope * string -> list((path * (string * TYPE)))
  resolveMutableProperty(s_entity, x) = typesOf(ps) :-
    query var filter EXTEND? (INHERIT EXTEND?)*
              and { x' :- x' == (x, _) }
              in s_entity |-> ps.

  declareParameters : scope * list((string * TYPE))
  declareParameters(s, []).
  declareParameters(s, [(x, t)|tail]) :-
    declareVar(s, x, t),
    declareAnnotation(s, x, DERIVED()), // abuse derived annotation to declare immutability of parameters
    declareParameters(s, tail).

rules // declare common rules and mappings

  unitOk : scope * Unit
  unitOk(_, EmptyUnit()).
  unitOk(_, _) :- try { false } | warning $[This unit is not yet implemented].

  sectionOk : scope * Section
  sectionOk(s, _) :- try { false } | warning $[This sections is not yet implemented].
  sectionsOk maps sectionOk(*, list(*))

  sectionOk(s, Section(_, defs)) :- defsOk(s, defs).

  defOk : scope * Definition
  defOk(_,_) :- try { false } | warning $[This definition is not yet implemented].

  defsOk maps defOk(*, list(*))

  stmtOk : scope * scope * Statement * TYPE // TYPE argument is the return type in a function
  stmtOk(_, _, stmt, _) :- try { false } | warning $[This statement is not yet implemented] @stmt.


  stmtsOk : scope * list(Statement) * TYPE
  stmtsOk(_, [], _).
  stmtsOk(s, [stmt | tail], rt) :- {s_decl s_next}
    new s_decl, s_decl -P-> s,
    new s_next, s_next -P-> s_decl,
    stmtOk(s, s_decl, stmt, rt),
    stmtsOk(s_next, tail, rt).

  expOk : scope * Exp
  expOk(s, exp) :- {T}
    typeOfExp(s, exp) == T.

  declareBuiltIns : scope
  declareBuiltIns(s) :-
    declareACBuiltIns(s),
    declareFunctionIns(s),
    declareEntityBuiltIns(s),
    declareTypeBuiltIns(s),
    declareTypeBuiltInFunctions(s),
    declareUIBuiltIns(s).

rules // typing

  typesOfExps maps typeOfExp(*, list(*)) = list(*)
  typeOfExp : scope * Exp -> TYPE
  typeOfExp(s, exp) = UNTYPED() :- try { false } | warning $[typing of expression [exp] is not yet implemented].

  typeOfExpTyped : scope * Exp -> TYPE
  typeOfExpTyped(s, exp) = t :-
    t == typeOfExp(s, exp),
    t != UNTYPED() | error $[Cannot resolve type [t]] @exp.

  typeOfSimpleExp : scope * SimpleExp -> TYPE
  typeOfSimpleExp(s, exp) = UNTYPED() :- try { false } | warning $[typing of expression [exp] is not yet implemented].

  typeOfPlaceholderExp : scope * PHExp -> TYPE
  typeOfPlaceholderExp(s, exp) = UNTYPED() :- try { false } | warning $[typing of expression [exp] is not yet implemented].

  typesCompatible : list(TYPE) * list(TYPE) -> BOOL
  typesCompatible([], []) = TRUE().
  typesCompatible([], [_|_]) = FALSE().
  typesCompatible([_|_], []) = FALSE().
  typesCompatible([t1|t1s], [t2|t2s]) = andB(typeCompatibleB(t1, t2), typesCompatible(t1s, t2s)).

  typesCompatibleWith maps typeCompatible(list(*), *)
  typeCompatible : TYPE * TYPE
  typeCompatible(T1, T2) :- typeCompatibleB(T1, T2) == TRUE().

  typeCompatibleB : TYPE * TYPE -> BOOL
  typeCompatibleB(T1, T2) = FALSE(). //default
  typeCompatibleB(T, T) = TRUE(). // same type is always type compatible

rules // utils

  or : BOOL * BOOL
  or(b1, b2) :- orB(b1, b2) == TRUE().
  orB : BOOL * BOOL -> BOOL
  orB(_, _) = FALSE().
  orB(TRUE(), _) = TRUE().
  orB(FALSE(), TRUE()) = TRUE().

  andB : BOOL * BOOL -> BOOL
  andB(_, _) = FALSE().
  andB(TRUE(), TRUE()) = TRUE().

  equalB : BOOL * BOOL
  equalB(b, b).
  equalB(_, _) :- false.

  notB : BOOL -> BOOL
  notB(TRUE()) = FALSE().
  notB(FALSE()) = TRUE().

  minOfList : list(int) -> int
  minOfList([]) = 100000.
  minOfList([x|xs]) = z :- {y}
    y == minOfList(xs),
    z #= min(x,y).

  scopeFromPath : path -> scope
  scopeFromPath(_PathEmpty(s)) = s.
  scopeFromPath(_PathStep(_, _, s)) = s.

  pathLength : path -> int
  pathLength(_PathEmpty(_)) = 0.
  pathLength(_PathStep(p, _, _)) = x :- {x'}
    x' == pathLength(p),
    x #= x' + 1.

  noDuplicatesString : string * list(string)
  noDuplicatesString(_, []).
  noDuplicatesString("_IGNORE", _).
  noDuplicatesString(x, ["_IGNORE" | ys]) :- noDuplicatesString(x, ys).
  noDuplicatesString(x, [y | ys]) :- noDuplicatesString(x, ys).
  noDuplicatesString(x, [x | _]) :- false.

  addQueryResults : list((path * (string * TYPE))) * list((path * (string * TYPE))) -> list((path * (string * TYPE)))
  addQueryResults([], ls) = ls.
  addQueryResults([hd | tl], ls) = [hd | addQueryResults(tl, ls)].

  // functional rule to drop all results with longer paths than the minimal
  filterShortestPath : list((path * (string * TYPE))) -> list((path * (string * TYPE)))
  filterShortestPath(ps) = filterShortestPath_internal(zipQueryResultsWithPathLength(ps), shortestPathLength(ps)).

  filterShortestPath_internal : list((int * (path * (string * TYPE)))) * int -> list((path * (string * TYPE)))
  filterShortestPath_internal([], _) = [].
  filterShortestPath_internal([(x, r) | ps], x) = [r | filterShortestPath_internal(ps, x)].
  filterShortestPath_internal([_      | ps], x) = filterShortestPath_internal(ps, x).

  shortestPathLength : list((path * (string * TYPE))) -> int
  shortestPathLength([]) = 10000.
  shortestPathLength([(p, _) | ps]) = z :- {x y}
    x == pathLength(p),
    y == shortestPathLength(ps),
    z #= min(x, y).

  zipQueryResultsWithPathLength maps zipQueryResultWithPathLength(list(*)) = list(*)
  zipQueryResultWithPathLength : (path * (string * TYPE)) -> (int * (path * (string * TYPE)))
  zipQueryResultWithPathLength(r@(p, _)) = (pathLength(p), r).

  queryResultTypes maps queryResultType(list(*)) = list(*)
  queryResultType : (path * (string * TYPE)) -> TYPE
  queryResultType((_, (_, t))) = t.

  argNames maps argName(list(*)) = list(*)
  argName : FormalArg -> string
  argName(Arg(x, _)) = x.

  sameElements : list(string) * list(string)
    sameElements1 : list(string) * list(string) * list(string)

  sameElements(l1, l2) :-
    sameElements1(l1, l2, l2),
    sameElements1(l2, l1, l1).

    sameElements1([], _, _) :- true.
    sameElements1([x | xs], [x | ys], completeYs) :- sameElements1(xs, completeYs, completeYs).
    sameElements1([x | xs], [y | ys], completeYs) :- sameElements1([x | xs], ys, completeYs).
    sameElements1([x | xs], []      , completeYs) :- false.

  typesOfStrings maps typeOfString(*, list(*)) = list(*)
  typeOfString : scope * string -> TYPE
  typeOfString(s, x) = t :-
    resolveVar(s, x) == [(_, (_, t))] | error $[Unable to find variable [x]].
