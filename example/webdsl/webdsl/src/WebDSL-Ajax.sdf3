module WebDSL-Ajax

imports
  WebDSL-Action
  WebDSL-Lexical
  WebDSL-UI
  WebDSL-Core

context-free sorts

  AjaxStatement Placeholder PlaceholderWithAjaxCall PlaceholderVar AjaxVisibility

context-free syntax

  Statement.AjaxStatement = <
    <AjaxStatement> ;
  >

  AjaxStatement.AjaxReplace = <replace ( <Exp> , <TemplateCall> )>
  AjaxStatement.AjaxAppend = <append ( <Exp> , <TemplateCall> )>
  AjaxStatement.AjaxVisibility = <visibility ( <Exp> , <AjaxVisibility> )>
  AjaxVisibility.Show = <show>
  AjaxVisibility.Toggle = <toggle>
  AjaxVisibility.Hide = <hide>
  AjaxStatement.AjaxRelocate = <relocate ( <PageCall> )>
  AjaxStatement.AjaxRestyle = <restyle ( <Exp> , <Exp> )>
  AjaxStatement.AjaxClear = <clear ( <Exp> )>
  AjaxStatement.AjaxRefresh = <refresh ( )>
  AjaxStatement.AjaxRunScript = <runscript ( <Exp> )>

context-free syntax

  ThisCall = <replace ( <Exp> , <TemplateCall> )> {reject}
  ThisCall = <append ( <Exp> , <TemplateCall> )> {reject}
  ThisCall = <visibility ( <Exp> , <AjaxVisibility> )> {reject}
  ThisCall = <relocate ( <PageCall> )> {reject}
  ThisCall = <restyle ( <Exp> , <Exp> )> {reject}
  ThisCall = <clear ( <Exp> )> {reject}
  ThisCall = <refresh ( )> {reject}
  ThisCall = <runscript ( <Exp> )> {reject}

context-free syntax

  TemplateElement.TEPlaceholder = <
    <Placeholder>
  >
  TemplateElement.TEPlaceholderWithAjaxCall = <
    <PlaceholderWithAjaxCall>
  >

  Placeholder.PlaceholderHtml = <placeholder \< <Id> \> <Id> {
    <TemplateElement*>
  }>
  Placeholder.PlaceholderHtmlExp = <placeholder \< <Id> \> <Exp> {
    <TemplateElement*>
  }>
  Placeholder.Placeholder = <placeholder <Id> {
    <TemplateElement*>
  }>
  Placeholder.PlaceholderExp = <placeholder <Exp> {
    <TemplateElement*>
  }>

  PlaceholderWithAjaxCall.PlaceholderAjaxHtml = <placeholder \< <Id> \> <Id> <TemplateCall>>
  PlaceholderWithAjaxCall.PlaceholderAjaxHtmlExp = <placeholder \< <Id> \> <PHExp> <TemplateCall>>
  PlaceholderWithAjaxCall.PlaceholderAjax = <placeholder <Id> <TemplateCall>>
  PlaceholderWithAjaxCall.PlaceholderAjaxExp = <placeholder <PHExp> <TemplateCall>>

  TemplateElement = PlaceholderVar

  PlaceholderVar.PlaceholderVar = <placeholdervar <Id>>

lexical syntax

  TemplateId = "placeholder" {reject}
  TemplateId = "placeholdervar" {reject}

lexical restrictions

  "placeholder" -/- [a-zA-Z0-9]

context-free priorities

  Placeholder.PlaceholderHtmlExp <4> .> Exp.Var,
  Placeholder.PlaceholderExp <1> .> Exp.Var

context-free sorts

  PHExp

context-free syntax

  PHExp.PHThisCall = ThisCall
  PHExp.PHCall = <<Exp> . <Id> ( <{Exp ","}*> )>
  PHExp.PHFieldAccess = <<Exp> . <PropertyId>>
  PHExp.PHCollectionIndex = <<Exp> [ <Exp> ]>
  PHExp.PHAdd = <<Exp> + <Exp>>
  PHExp.PHConst = ConstValue
