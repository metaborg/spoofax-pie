module WebDSL-Action

imports
  WebDSL-Core
  WebDSL-DataModel
  WebDSL-Lexical
  WebDSL-Search
  WebDSL-UI
  HQL

context-free sorts

  OptSort Block Function

context-free syntax

  Function.Function = <
    function <FunId> <OptFormalArgs> <OptSort> <Block>
  >
  OptSort.OptSortNone = <>
  OptSort.OptSortSome = <: <Sort>>
  Definition.GlobalFunction = <
    <Function>
  >
  Definition.CachedGlobalFunction = <cached <Function>>
  Definition.ExtendGlobalFunction = <extend <Function>>
  EntityBodyDeclaration.EntityFunction = <<Function>>
  EntityBodyDeclaration.CachedFunction = <cached <Function>>
  EntityBodyDeclaration.ExtendFunction = <extend <Function>>
  EntityBodyDeclaration.StaticEntityFunction = <static <Function>>

context-free syntax

  Exp.FunctionExp = <function <OptFormalArgs> <OptSort> <Block>>
  Exp.FunctionRef = <function . <FunId> ( <{Sort ","}*> ) <OptSort>>
  Exp.FunctionRefCall = <function . <FunId> ( <{Sort ","}*> ) <OptSort> ( <{Exp ","}*> )>
  Exp.FunctionRefCallPartial = <function . <FunId> ( <{Sort ","}*> ) <OptSort> ( * , <{Exp ","}*> )>

lexical syntax

  VarId = "function" {reject}

context-free syntax

  Block.Block = <{
    <Statement*>
  }>
  Statement = Block
  Statement.Stat = <
    <Exp>;
  >
  Statement.Return = <
    return <Exp>;
  >
  Statement.ReturnEmpty = <
    return;
  >

lexical syntax

  VarId = "return" {reject}
  FunId = "return" {reject}

context-free priorities

  Statement.Stat <0> .> Exp.Var

context-free syntax

  Statement.GoTo = <
    goto <PageCall>;
  >
  Statement.GoToAlt = <
    goto ( <PageCall> );
  >

  ThisCall = <goto ( <PageCall> )> {reject}

  Statement.VarDecl = <
    var <VarId> : <Sort>;
  >
  Statement.VarDeclInit = <
    var <VarId> : <Sort> := <Exp>;
  >
  Statement.VarDeclInitInferred = <
    var <VarId> := <Exp>;
  >

context-free sorts

  ElseIfStatement OptElse Case TypeCaseAlt OptTypeAlias

context-free syntax

  Statement.If = <
    if ( <Exp> ) <Block> else <Block>
  >
  Statement.IfNoElse = <
    if ( <Exp> ) <Block>
  >
  Statement.IfWithElseIf = <
    if ( <Exp> ) <Block> <ElseIfStatement+> <OptElse>
  >
  ElseIfStatement.ElseIf = <else if ( <Exp> ) <Block>>
  OptElse.OptElseSome = <else <Block>>
  OptElse.OptElseNone = <>

  Statement.CaseStmt = <
    case ( <{Exp ","}+> ) {
      <Case*>
    }
  >
  Case.CaseAlt = <
    <CaseAltExps> <Block>
  >
  Case.CaseAltDefault = <
    default <Block>
  >

  Statement.TypeCaseStmt = <
    typecase ( <Exp> <OptTypeAlias> ) {
      <TypeCaseAlt*>
    }
  >
  OptTypeAlias.TypeAlias = <as <Id>>
  OptTypeAlias.OptTypeAliasNone = <>
  TypeCaseAlt.TypeCaseAlt = <
    <Sort> <Block>
  >
  TypeCaseAlt.TypeCaseAltDefault = <
    default <Block>
  >

context-free sorts

  CaseAltExps

context-free syntax

  CaseAltExps.CaseExps = <<{Exp ","}+>>
  CaseAltExps = <default> {reject}

context-free priorities

  Statement.TypeCaseStmt <2> .> Exp.Cast

context-free syntax

  Statement.ForStmt = <
    for ( <VarId> : <Sort> in <Exp> <OptFilter> ) <Block>
  >
  Statement.ForStmtInferred = <
    for ( <VarId> in <Exp> <OptFilter> ) <Block>
  >
  Statement.ForAllStmt = <
    for ( <VarId> : <Sort> <OptFilter> ) <Block>
  >
  Statement.ForCountStmt = <
    for ( <VarId> : Int from <Exp> to <Exp> ) <Block>
  >
  Statement.WhileStmt = <
    while ( <Exp> ) <Block>
  >

lexical sorts

  ThisLex

lexical syntax

  ThisLex = "this"

lexical restrictions

  ThisLex -/- [a-zA-Z0-9]

context-free sorts

  ConstValue Exp PropAssignment ObjectPropertyAssignment Mapping ThisCall FAExp

context-free syntax

  Exp.Const = ConstValue

  ConstValue.Int = Int
  ConstValue.Long = Long
  ConstValue.Float = Float
  ConstValue.StringConst = String

  Exp.Var = VarId
  Exp.FieldAccess = <<FAExp> . <PropertyId>>
  Exp.CollectionIndex = <<Exp> [ <Exp> ]>
  Exp.This = <<ThisLex>>

  FAExp.FAExp = <<Exp>>
  FAExp.FAVar = <<FAVarId>>
  FAExp.FAGlobal = <global>
  FAExp.FASession = <session>

context-free priorities

  FAExp.FAExp <0> .> Exp.Var

lexical syntax

  VarId = "this" {reject}
  PropertyId = "this" {reject}

  FAVarId = "global" {reject}
  FAVarId = "session" {reject}

context-free syntax

  Exp.ObjectCreation = <<SimpleSortLex> {
    <PropAssignment*>
  }>

  PropAssignment.PropAssignment = <<ObjectPropertyAssignment>>
  PropAssignment.PropAssignmentComma = <<ObjectPropertyAssignment> ,>
  ObjectPropertyAssignment.ObjectPropertyAssignment = <
    <Id> := <Exp>
  >

/*
  used in sdf2 definition as workaround to get working autocompletion:
  ObjectPropertyAssignment.IncompleteObjectPropertyAssignment = <
    <Id>
  >
*/

  Statement.Assignment = <
    <Exp> := <Exp> ;
  >

  Exp.MapCreation = <[ <Mapping+> ]>
  Mapping.Mapping = <<Exp> -\> <Exp>>
  Exp.ListCreation = <[ <{Exp ","}*> ]>
  Exp.SetCreation = <{ <{Exp ","}+> }>
  Exp.TypedListCreation = <List \< <Sort> \> ( <{Exp ","}*> )>
  Exp.TypedSetCreation = <Set \< <Sort> \> ( <{Exp ","}*> )>

lexical syntax

  VarId = "List" {reject}
  VarId = "Set" {reject}

context-free syntax

  Exp = ThisCall
  ThisCall.ThisCall = <<FunId> ( <{Exp ","}*> )>
  Exp.Call = <<Exp> . <Id> ( <{Exp ","}*> )>
  Exp.IsA = <<Exp> is a <Sort>>
  Exp.Cast = <<Exp> as <Sort>>
  Exp.InColl = <<Exp> in <Exp>>

  Exp.IfExp = <if ( <Exp> ) <Exp> else <Exp>>

  OutputShorthand.OutputShorthand = <~ ( <Exp> )>
  SimpleExp.SimpleThisCall = ThisCall
  SimpleExp.SimpleCall = <<SimpleExp> . <Id> ( <{Exp ","}*> )>

lexical syntax

  VarId = "if" {reject}
  VarId = "else" {reject}

syntax

  StringPart-CF.InterpExp = "~" "(" Exp-CF ")"

context-free restrictions

  "in" -/- [a-zA-Z0-9\_]

context-free sorts

  OptGroupBy GroupBy ForExp

context-free syntax

  Exp.Eq = <<Exp> == <Exp>> {left}
  Exp.NotEq = <<Exp> != <Exp>> {left}
  Exp.LargerThan = <<Exp> \> <Exp>> {left}
  Exp.LargerThanOrEqual = <<Exp> \>= <Exp>> {left}
  Exp.SmallerThan = <<Exp> \< <Exp>> {left}
  Exp.SmallerThanOrEqual = <<Exp> \<= <Exp>> {left}

  Exp.True = <true>
  Exp.False = <false>
  Exp.Null = <null>

lexical syntax

  VarId = "true" {reject}
  VarId = "false" {reject}
  VarId = "null" {reject}

  TemplateId = "true" {reject}
  TemplateId = "false" {reject}
  TemplateId = "null" {reject}

context-free syntax

  Exp.And = <<Exp> && <Exp>> {left}
  Exp.Or = <<Exp> || <Exp>> {left}
  Exp.Not = <! <Exp>>
  Exp = <( <Exp> )> {bracket}
  ForExp.ForExp = <[ <Exp> | <VarId> <OptSort> in <Exp> <OptFilter> <OptGroupBy> ]>
  ForExp.ForExpShortSort = <[ <VarId> : <Sort> in <Exp> ]>
  ForExp.ForExpShortFilter = <[ <VarId> <OptSort> in <Exp> <Filter> ]>
  ForExp.ForExpShortGroupBy = <[ <VarId> <OptSort> in <Exp> <GroupBy> ]>
  ForExp.ForExpShort = <[ <VarId> <OptSort> in <Exp> <Filter> <GroupBy> ]>
  Exp = ForExp
  OptGroupBy.OptGroupBySome = GroupBy
  OptGroupBy.OptGroupByNone = <>
  GroupBy.GroupBy = <group by <Exp>>
  Exp.AndForExp = <And <ForExp>>
  Exp.OrForExp = <Or <ForExp>>
  Exp.Mul = <<Exp> * <Exp>> {left}
  Exp.Div = <<Exp> / <Exp>> {left}
  Exp.Mod = <<Exp> % <Exp>> {left}
  Exp.Add = <<Exp> + <Exp>> {left}
  Exp.Sub = <<Exp> - <Exp>> {left}

context-free sorts

  HqlExp LimitValueExp DslExp

context-free syntax

  Exp.HqlExp = HqlExp

  HqlExp.HqlQuery             = <<HQLQueryRule>>
  HqlExp.HqlQueryLimit        = <<HQLQueryRule> limit <LimitValueExp>>
  HqlExp.HqlQueryLimitOffset  = <<HQLQueryRule> limit <LimitValueExp> , <LimitValueExp>>

  LimitValueExp.LimitValueExp     = <<Exp>>
  LimitValueExp.LimitValueDslExp  = <<DslExp>>

  DslExp.DslExp = <~ <Exp>>

  Statement.HqlStatement = <
    <HQLDeleteStatement> ;
  >

context-free priorities

  {left :
    Exp.Call
    Exp.FieldAccess
    Exp.CollectionIndex
  } >
  {non-assoc :
    Exp.IsA
    Exp.Cast
    Exp.InColl
  } >
  {
    Exp.Not
  } >
  {left :
    Exp.Mul
    Exp.Div
    Exp.Mod
  } >
  {left:
    Exp.Add
    Exp.Sub
  } >
  {left :
    Exp.Eq
    Exp.NotEq
    Exp.LargerThan
    Exp.LargerThanOrEqual
    Exp.SmallerThan
    Exp.SmallerThanOrEqual
  } >
  { Exp.RetrievalExp } >
  {left : Exp.And } >
  {left : Exp.Or } >
  { Exp.HqlExp } >
  { Exp.SearcherDef } >
  { Exp.IfExp }

context-free sorts

  EmailCall

context-free syntax

  Exp.RenderEmailFunctionCall = <renderemail ( <EmailCall> )>
  Exp.EmailFunctionCall = <email ( <EmailCall> )>
  Exp.EmailFunctionCallAlt = <email <EmailCall>>
  Exp.SendEmailFunctionCall = <sendemail ( <EmailCall> )>
  EmailCall.EmailCall = <<Id> ( <{Exp ","}*> )>

  Exp.RenderTemplateFunctionCall = <rendertemplate ( <TemplateCall> )>
  Exp.ValidateTemplateFunctionCall = <validatetemplate ( <TemplateCall> )>

context-free syntax

  ThisCall = <renderemail ( <EmailCall> )> {reject}
  ThisCall = <email ( <EmailCall> )> {reject}
  ThisCall = <email <EmailCall>> {reject}
  ThisCall = <sendemail ( <EmailCall> )> {reject}

  ThisCall = <rendertemplate ( <TemplateCall> )> {reject}
  ThisCall = <validatetemplate ( <TemplateCall> )> {reject}
