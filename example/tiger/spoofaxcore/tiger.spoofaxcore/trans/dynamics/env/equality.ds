module dynamics/env/equality

imports ds-signatures/Numbers-sig 
imports dynamics/env/numbers
imports dynamics/env/strings
imports dynamics/env/control-flow
imports dynamics/env/records
imports dynamics/env/natives
imports dynamics/env/arrays

rules

  Eq(v1, v2) --> v
  where
    case v1 of {
      IntV(i) -> 
        case v2 of {
           IntV(j) -> 
             IntV(eqI(i, j)) => v
        }
      StringV(x) ->
        case v2 of {
          StringV(y) -> 
            IntV(eqS(x, y)) => v
        }
      NilV() ->
        case v2 of {
          NilV() -> 
            IntV(1) => v
          RecordV(_, _) ->
            IntV(0) => v
        }
      RecordV(_, id1) ->
        case v2 of {
          NilV() -> 
            IntV(0) => v
          RecordV(_, id2) ->
            IntV(eqI(id1, id2)) => v
        }
      ArrayV(_, id1) ->
        case v2 of {
          ArrayV(_, id2) ->
            IntV(eqI(id1, id2)) => v
          otherwise ->
            IntV(0) => v
        }     
    }
      
  Neq(e1, e2) --> If(Eq(e1, e2), Int("0"), Int("1"))
  
  And(e1, e2) --> If(e1, e2, Int("0"))
  
  Or(e1, e2) --> If(e1, Int("1"), e2)
  