module dynamics/env/predef

imports ds-signatures/Tiger-sig
imports dynamics/env/control-flow
imports dynamics/env/base
imports dynamics/env/natives
imports dynamics/env/store
imports dynamics/env/bindings
imports dynamics/env/functions

signature

  constructors
    nativePrintV     : Exp
    nativePrintIV     : Exp
    nativeOrdV       : Exp
    nativeChrV       : Exp
    nativeNotV       : Exp
    nativeExitV      : Exp
    nativeSizeV      : Exp
    nativeSubstringV : Exp
    nativeConcatV    : Exp
    nativeGetcharV   : Exp
    nativeFlushV     : Exp
    nativeStartTiming : Exp
    nativeStopTiming  : Exp
  
  arrows
    E |- stdLib() :: H --> Env :: H

rules

  // predefined functions  
  E |- stdLib() --> E'
  where
    E  |- bindVar("print",     ClosureV([FArg(__Id2Occ__("s"), Tid(__Id2Occ__("string")))], nativePrintV(), E)) --> E1;
    E1 |- bindVar("ord",       ClosureV([FArg(__Id2Occ__("s"), Tid(__Id2Occ__("string")))], nativeOrdV(), E)) --> E2;
    E2 |- bindVar("chr",       ClosureV([FArg(__Id2Occ__("i"), Tid(__Id2Occ__("int")))], nativeChrV(), E)) --> E3;
    E3 |- bindVar("not",       ClosureV([FArg(__Id2Occ__("i"), Tid(__Id2Occ__("int")))], nativeNotV(), E)) --> E4;
    E4 |- bindVar("exit",      ClosureV([FArg(__Id2Occ__("i"), Tid(__Id2Occ__("int")))], nativeExitV(), E)) --> E5;
    E5 |- bindVar("size",      ClosureV([FArg(__Id2Occ__("s"), Tid(__Id2Occ__("string")))], nativeSizeV(), E)) --> E6;
    E6 |- bindVar("substring", ClosureV([FArg(__Id2Occ__("s"), Tid(__Id2Occ__("string"))), FArg(__Id2Occ__("i1"), Tid(__Id2Occ__("int"))), FArg(__Id2Occ__("i2"), Tid(__Id2Occ__("int")))], nativeSubstringV(), E)) --> E7;
    E7 |- bindVar("concat",    ClosureV([FArg(__Id2Occ__("s1"), Tid(__Id2Occ__("string"))), FArg(__Id2Occ__("s2"), Tid(__Id2Occ__("string")))], nativeConcatV(), E)) --> E8;
    E8 |- bindVar("getchar",   ClosureV([], nativeGetcharV(), E)) --> E9;
    E9 |- bindVar("flush",     ClosureV([], nativeFlushV(), E)) --> E10;
    E10|- bindVar("printi",    ClosureV([FArg(__Id2Occ__("i"), Tid(__Id2Occ__("int")))], nativePrintIV(), E)) --> E11;
    E11|- bindVar("timeGo",    ClosureV([], nativeStartTiming(), E)) --> E12;
    E12|- bindVar("timeStop",  ClosureV([], nativeStopTiming(), E)) --> E'
  
  // predefined function reductions
  nativePrintV() --> UnitV()
  where
    read(lookup("s")) => StringV(s);
    printS(s) => _

  nativePrintIV() --> UnitV()
  where
    read(lookup("i")) => IntV(i);
    printI(i) => _

  nativeOrdV() --> IntV(result)
  where
    read(lookup("s")) => StringV(s);
    ordS(s) => result

  nativeChrV() --> StringV(result)
  where
    read(lookup("i")) => IntV(i);
    chrI(i) => result
    
  nativeNotV() --> IntV(result)
  where
    read(lookup("i")) => IntV(i);
    notI(i) => result
  
  nativeExitV() --> UnitV()
  where
    read(lookup("i")) => IntV(i);
    notI(i) => _
    
  nativeSizeV() --> IntV(result)
  where
    read(lookup("s")) => StringV(s);
    sizeS(s) => result
  
  nativeSubstringV() --> StringV(result)
  where
    read(lookup("s")) => StringV(s);
    read(lookup("i1")) => IntV(i1);
    read(lookup("i2")) => IntV(i2);
    substringSII(s, i1, i2) => result
  
  nativeConcatV() --> StringV(result)
  where
    read(lookup("s1")) => StringV(s1);
    read(lookup("s2")) => StringV(s2);
    concatS(s1, s2) => result
  
  nativeStartTiming() --> UnitV()
  where
    _startTiming() => _
  
  nativeStopTiming() --> IntV(_stopTiming())
  
  nativeGetcharV() --> StringV(result)
  where
    getcharS("") => result
  
  nativeFlushV() --> UnitV()
  where
    flushS("") => _
  