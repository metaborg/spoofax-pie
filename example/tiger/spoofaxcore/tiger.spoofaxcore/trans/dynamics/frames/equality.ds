module dynamics/frames/equality

imports ds-signatures/Numbers-sig 
imports dynamics/frames/numbers
imports dynamics/frames/strings
imports dynamics/frames/control-flow
imports dynamics/frames/records
imports dynamics/frames/natives
imports dynamics/frames/arrays

rules

  Eq(vv1, vv2) --> vv
  where
    case vv1 of {
      IntV(i) -> 
        case vv2 of {
           IntV(j) -> 
             IntV(eqI(i, j)) => vv
        }
      StringV(x) ->
        case vv2 of {
          StringV(y) -> 
            IntV(eqS(x, y)) => vv
        }
      NilV() ->
        case vv2 of {
          NilV() -> 
            IntV(1) => vv
          RecordV(_) ->
            IntV(0) => vv
        }
      RecordV(F_(f1)) ->
        case vv2 of {
          NilV() -> 
            IntV(0) => vv
          RecordV(F_(f2)) ->
            IntV(eqI(f1, f2)) => vv
        }
      ArrayV(F_(f1)) ->
        case vv2 of {
          ArrayV(F_(f2)) ->
            IntV(eqI(f1, f2)) => vv
          otherwise ->
            IntV(0) => vv
        }    
    }
      
  Neq(e1, e2) --> If(Eq(e1, e2), Int("0"), Int("1"))
  
  And(e1, e2) --> If(e1, e2, Int("0"))
  
  Or(e1, e2) --> If(e1, Int("1"), e2)
  